buildscript {
  repositories {
    jcenter()
    maven { url "http://dl.bintray.com/spinnaker/gradle" }
  }
  dependencies {
    classpath 'com.netflix.spinnaker.gradle:spinnaker-gradle-project:3.1.0'
  }
}

group = "com.netflix.spinnaker.spinnaker"


apply plugin: 'spinnaker.project'
apply plugin: 'nebula.ospackage'
apply plugin: 'java'

String toVers(String v) {
  int idx = v.indexOf('-')
  if (idx != -1) {
    return v.substring(0, idx)
  }
  return v
}

ospackage {

// common installation paths 
  def spinnakerHome = "/opt/" + project.name

// global package requirements
  packageName = project.name
  version = toVers(project.version.toString())
  release '3'

// files to put in the meta package
  from('pylib') {
    into spinnakerHome + '/pylib'
  }

  from('cassandra') {
    into spinnakerHome + '/cassandra'
  }

  from('install') {
    into spinnakerHome + '/install'
  }

  from('runtime') {
    into spinnakerHome + '/bin'
    fileMode = 0755
  }

  from('config') {
    into spinnakerHome + '/config'
    fileMode = 0644
  }

  File configDir
  FileCollection collection = files { configDir.listFiles() }

  configDir = file('config')
  collection.each { configurationFile(spinnakerHome + '/' + relativePath(it) + '\n') }
}

// Ubuntu
buildDeb {
  // installer scripts
  preInstall = file('pkg_scripts/deb/preInstall.sh')
  postInstall = file('pkg_scripts/deb/postInstall.sh')
  postUninstall = file('pkg_scripts/deb/postUninstall.sh')

  from(file('pkg_files/deb/etc/init/spinnaker.conf')) {
    into('/etc/init')
    user = 'root'
    permissionGroup = 'root'
    fileType = CONFIG | NOREPLACE
  }

  from('pkg_files/deb/etc/apache2/sites-available/spinnaker.conf') {
    into '/etc/apache2/sites-available'
    user = 'root'
    permissionGroup = 'root'    
    fileMode = 0644
  }

  from('pkg_files/deb/etc/default/spinnaker') {
    into '/etc/default'
    user = 'root'
    permissionGroup = 'root'
    fileMode = 0644
  }

  configurationFile('/etc/default/spinnaker\n')

  File configDir
  FileCollection collection = files { configDir.listFiles() }

  configDir = file('config')
  collection.each { configurationFile(spinnakerHome + '/' + relativePath(it) + '\n') }
}

// TO DO 
buildRpm {
  arch = NOARCH
  os = LINUX
  // installer scripts
  preInstall = file('pkg_scripts/rpm/preInstall.sh')
  postInstall = file('pkg_scripts/rpm/postInstall.sh')
  postUninstall = file('pkg_scripts/rpm/postUninstall.sh')

  from(file('pkg_files/rpm/etc/init/spinnaker.conf')) {
    into('/etc/init')
    user = 'root'
    permissionGroup = 'root'
    fileType = CONFIG | NOREPLACE
  }

  from('pkg_files/rpm/etc/httpd/conf.d/spinnaker.conf') {
    into '/etc/httpd/conf.d'
    user = 'root'
    permissionGroup = 'root'
    fileMode = 0644
  }

  from('pkg_files/rpm/etc/sysconfig/spinnaker') {
    into '/etc/sysconfig'
    user = 'root'
    permissionGroup = 'root'
    fileMode = 0644
  }

  configurationFile('/etc/sysconfig/spinnaker\n')

  requires('java-1.8.0-openjdk')
  // requires('redis-server', '3.0.5', GREATER | EQUAL)
  requires('spinnaker-clouddriver')
  requires('spinnaker-deck')
  requires('spinnaker-echo')
  requires('spinnaker-front50')
  requires('spinnaker-gate')
  requires('spinnaker-igor')
  requires('spinnaker-orca')
  requires('spinnaker-rosco')
  requires('spinnaker-rush')
  requires('httpd24')
}
